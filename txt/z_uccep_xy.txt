z_uccep_xy1:--z_uccep_xy1
declare @t_bstoreno nvarchar(50)
declare @t_estoreno nvarchar(50)
declare @t_bproductno nvarchar(50)
declare @t_eproductno nvarchar(50)
declare @t_typea nvarchar(50)
declare @t_groupa nvarchar(50)
declare @t_btggno nvarchar(50)
declare @t_etggno nvarchar(50)
declare @t_xedate nvarchar(50)
declare @t_zero nvarchar(50)
declare @t_uccno nvarchar(MAX)
declare @t_product nvarchar(MAX)
declare @t_style nvarchar(MAX)
declare @t_spec nvarchar(MAX)
declare @t_rank nvarchar(50)

set @t_rank = '[2]'
set @t_bproductno = case when '#non' = [5] then '' else [5] end
set @t_eproductno = case when '#non' = [6] then CHAR(255) else [6] end
set @t_bstoreno = case when '#non' = [7] then '' else [7] end
set @t_estoreno = case when '#non' = [8] then CHAR(255) else [8] end
set @t_typea = case when '#non' = [9] then '' else [9] end
set @t_groupa = case when '#non' = [10] then '' else [10] end
set @t_btggno = case when '#non' = [11] then '' else [11] end
set @t_etggno = case when '#non' = [12] then char(255) else [12] end
set @t_xedate = case when '#non' = [13] then '' else [13] end
set @t_zero = case when '#non' = [14] then '' else [14] end
set @t_uccno = case when '#non' = [15] then '' else [15] end
set @t_product = case when '#non' = [16] then '' else [16] end
set @t_style = case when '#non' = [17] then '' else [17] end
set @t_spec = case when '#non' = [18] then '' else [18] end

--************************************************************************
declare @t_pedate nvarchar(50)=dbo.q_cdn(@t_xedate,-1)

declare @stmp table(
	pno nvarchar(50),
	storeno nvarchar(100),
	smount float ----倉庫總量
)

insert @stmp(pno,storeno,smount)
select productno,storeno,mount
from stkucc(@t_pedate,'','') a 
where exists (select * from ucc where noa=a.productno)
and productno between @t_bproductno and @t_eproductno
and (len(@t_uccno)=0 or '.'+@t_uccno+'.' like '%.'+productno+'.%')

declare @vtmp table(
	pno nvarchar(50),
	storeno nvarchar(100),
	vmount float ----客倉總量
)
insert @vtmp(pno,storeno,vmount)
select productno,storeno2,round(SUM(case when a.typea='1' then 1 else -1 end *(tranmoney2-tranmoney3)),2) mount
from view_vccs a
where (itemno='1' or itemno='2') and (tranmoney2!=0 or tranmoney3!=0) and datea<=@t_pedate
and productno between @t_bproductno and @t_eproductno
and (len(@t_uccno)=0 or '.'+@t_uccno+'.' like '%.'+productno+'.%')
group by productno,storeno2
having round(SUM(case when a.typea='1' then 1 else -1 end *(tranmoney2-tranmoney3)),2)!=0

declare @utmp table(
	pno nvarchar(50),
	storeno nvarchar(100),
	emount float, ----ucce帳面總量
	mount float, ----盤點量
	amount float ----app盤點量
)
insert @utmp(pno,storeno,emount,mount,amount)
select b.productno,b.storeno,sum(b.emount2),sum(b.mount),SUM(dime)
from view_ucce a left join view_ucces b on a.noa=b.noa
where a.datea=@t_xedate
and productno between @t_bproductno and @t_eproductno
and (len(@t_uccno)=0 or '.'+@t_uccno+'.' like '%.'+productno+'.%')
group by b.productno,b.storeno

declare @tmp table(
	gno nvarchar(1),
	page int,
	recno int,
	pno nvarchar(50),
	product nvarchar(255),
	custno nvarchar(50),
	comp nvarchar(100),
	style nvarchar(100),
	spec nvarchar(MAX),
	storeno nvarchar(100),
	store nvarchar(100),
	tggno nvarchar(50),
	tgg nvarchar(200),
	unit nvarchar(50),
	smount float, ----倉庫總量
	vmount float, ----客倉總量
	emount float, ----帳面總量(倉庫總量+客倉總量)
	mount float, ----盤點量(app盤點量)
	dmount float, ----盤差量
	safem float, ----安全存量
	img nvarchar(max),
	ucce nvarchar(50)
)

--插入庫存量
insert @tmp(gno,pno,storeno,smount,emount)
select '9',pno,storeno,smount,smount from @stmp

--插入客倉
insert @tmp(gno,pno,storeno,vmount,emount)
select '9',pno,'A',sum(vmount),sum(vmount) from @vtmp group by pno

--插入帳面和盤點量
insert @tmp(gno,pno,storeno,emount,mount,ucce)
select '9',pno,storeno,0,round(amount,2),'V' from @utmp

insert @tmp(gno,pno,storeno,smount,vmount,emount,mount,dmount,ucce)
select '1',a.pno,a.storeno,isnull(SUM(a.smount),0),isnull(SUM(a.vmount),0),isnull(SUM(a.emount),0)
,isnull(SUM(a.mount),0),isnull(SUM(a.emount),0)-isnull(SUM(a.mount),0),MAX(a.ucce)
from @tmp a left join ucc b on a.pno=b.noa where gno='9' 
and a.storeno between @t_bstoreno and @t_estoreno
and (len(@t_typea)=0 or b.typea=@t_typea)
and (len(@t_groupa)=0 or b.groupano=@t_groupa)
and isnull(b.tggno,'') between @t_btggno and @t_etggno
and (len(@t_style)=0 or b.style=@t_style)
and (len(@t_product)=0 or b.product like '%'+@t_product+'%')
and (len(@t_spec)=0 or b.spec like '%'+@t_spec+'%')
group by a.pno,a.storeno

if(@t_zero='1')
begin
	insert @tmp(gno,pno,storeno,smount,vmount,emount,mount,dmount)
	select '1',noa,'A',0,0,0,0,0 from ucc a
	where noa between @t_bproductno and @t_eproductno
	and not exists (select * from @tmp where pno=a.noa)
	and (len(@t_typea)=0 or typea=@t_typea)
	and (len(@t_groupa)=0 or groupano=@t_groupa)
	and isnull(tggno,'') between @t_btggno and @t_etggno
	and (len(@t_uccno)=0 or '.'+@t_uccno+'.' like '%.'+noa+'.%')
	and (len(@t_style)=0 or style=@t_style)
	and (len(@t_product)=0 or product like '%'+@t_product+'%')
	and (len(@t_spec)=0 or spec like '%'+@t_spec+'%')
end

delete @tmp where gno='9'

update a
set gno='0' 
,product=b.product
,style=b.style
,spec=b.spec
,safem=isnull(b.safemount,0)
,store=c.store
,custno=isnull(d.noa,'')
,comp=isnull(d.nick,'')
,img='<img width="50px" src="https://chart.googleapis.com/chart?chs=100x100&cht=qr&chl='+upper(a.pno)+'&chld=H|0">'
,tggno=isnull(b.tggno,'')
,tgg=isnull(b.tgg,'')
,unit=isnull(b.unit,'')
from @tmp a left join ucc b on a.pno=b.noa
left join store c on a.storeno=c.noa
left join cust d on left(pno,5)=d.noa and CHARINDEX('-',pno)>0

--分頁
declare @t_pageline int=12
update a
set recno=rr,page=ceiling(cast(rr as float)/cast(@t_pageline as float))
from (select *,ROW_NUMBER()over (order by pno)rr from @tmp) a

if(@t_rank>5)
begin
	insert @tmp(gno,page,recno)
	select '1',page,MIN(recno)-1 from @tmp where gno='0' group by page
	insert @tmp(gno,page,recno)
	select '3',page,MAX(recno)+1 from @tmp where gno='0' group by page
	update @tmp set gno='2' where gno='0'
end
else
begin
	insert @tmp(gno,page,recno)
	select '4',page,MIN(recno)-1 from @tmp where gno='0' group by page
	insert @tmp(gno,page,recno)
	select '6',page,MAX(recno)+1 from @tmp where gno='0' group by page
	update @tmp set gno='5' where gno='0'
end

select 
dbo.getComma(smount,-1)smount
,dbo.getComma(vmount,-1)vmount
,dbo.getComma(emount,-1)emount
,dbo.getComma(case when ucce is null then null else mount end,-1)mount
,dbo.getComma(dmount,-1)dmount
,dbo.getComma(safem,-1)safem
,*
 from @tmp order by page,gno,recno;
